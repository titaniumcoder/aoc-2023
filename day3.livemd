# Day 3

```elixir
Mix.install([
  {:kino, "~> 0.11.3"}
])
```

## Part 1

You and the Elf eventually reach a gondola lift station; he says the gondola lift will take you up to the water source, but this is as far as he can bring you. You go inside.

It doesn't take long to find the gondolas, but there seems to be a problem: they're not moving.

"Aaah!"

You turn around to see a slightly-greasy Elf with a wrench and a look of surprise. "Sorry, I wasn't expecting anyone! The gondola lift isn't working right now; it'll still be a while before I can fix it." You offer to help.

The engineer explains that an engine part seems to be missing from the engine, but nobody can figure out which one. If you can add up all the part numbers in the engine schematic, it should be easy to work out which part is missing.

The engine schematic (your puzzle input) consists of a visual representation of the engine. There are lots of numbers and symbols you don't really understand, but apparently any number adjacent to a symbol, even diagonally, is a "part number" and should be included in your sum. (Periods (.) do not count as a symbol.)

Here is an example engine schematic:

```
467..114..
...*......
..35..633.
......#...
617*......
.....+.58.
..592.....
......755.
...$.*....
.664.598..
```

In this schematic, two numbers are not part numbers because they are not adjacent to a symbol: 114 (top right) and 58 (middle right). Every other number is adjacent to a symbol and so is a part number; their sum is 4361.

Of course, the actual engine schematic is much larger. What is the sum of all of the part numbers in the engine schematic?

## Solution

```elixir
defmodule Parser do
  defmodule Number do
    defstruct [:number, :start, :end]
  end

  defmodule Line do
    # Cubes are a list of tuples, each tuple in {r, g, b}
    defstruct [:numbers, :symbols, :adjacents]
  end

  def parse_line(line) do
    parse_position(line, 0, nil, %Parser.Line{numbers: [], symbols: [], adjacents: []})
  end

  defp parse_position("", _position, number, line),
    do: add_number_to_line(line, number)

  defp parse_position("." <> rest, position, number, line) do
    parse_position(rest, position + 1, nil, add_number_to_line(line, number))
  end

  defp parse_position(
         <<next::binary-1, rest::binary>>,
         position,
         current_number,
         line
       ) do
    case Integer.parse(next) do
      {int, ""} ->
        handle_number(int, rest, position, current_number, line)

      :error ->
        parse_symbol(next, rest, position, current_number, line)
    end
  end

  defp parse_symbol(symbol, rest, position, number, line) do
    new_line =
      put_in(line.symbols, [position | line.symbols])
      |> add_number_to_line(number)

    final_new_line =
      case symbol do
        "*" ->
          put_in(new_line.adjacents, [position | new_line.adjacents])

        _ ->
          new_line
      end

    parse_position(rest, position + 1, nil, final_new_line)
  end

  defp handle_number(number, rest, position, nil, line) do
    new_number = %Parser.Number{number: number, start: position, end: position}
    parse_position(rest, position + 1, new_number, line)
  end

  defp handle_number(number, rest, position, current_number, line) do
    new_number = %{current_number | number: current_number.number * 10 + number, end: position}
    parse_position(rest, position + 1, new_number, line)
  end

  defp add_number_to_line(line, nil), do: line

  defp add_number_to_line(line, number) do
    Kernel.put_in(line.numbers, [number | line.numbers])
  end
end
```

```elixir
Parser.parse_line("467..114..")
```

```elixir
Parser.parse_line("...*......")
```

```elixir
Parser.parse_line("617*......")
```

```elixir
defmodule Part1 do
  def solution(input) do
    input
    |> String.trim()
    |> String.split("\n")
    |> Enum.map(&Parser.parse_line/1)
    |> calculate_numbers(nil, [])
    |> Enum.sum()
  end

  defp calculate_numbers([], _, agg), do: agg

  defp calculate_numbers([hd | tl], previous_line, agg) do
    adjacent_symbol_positions =
      Enum.flat_map([previous_line, hd, List.first(tl)], &extract_smybols/1)

    line_numbers = extract_adjacent_numbers(hd, adjacent_symbol_positions)
    calculate_numbers(tl, hd, agg ++ line_numbers)
  end

  defp extract_smybols(nil), do: []
  defp extract_smybols(%Parser.Line{symbols: symbols}), do: symbols

  defp extract_adjacent_numbers(%Parser.Line{numbers: numbers}, symbol_positions) do
    numbers
    |> Enum.filter(fn %{start: min, end: max} ->
      symbol_position_adjacent(symbol_positions, (min - 1)..(max + 1))
    end)
    |> Enum.map(fn %{number: number} -> number end)
  end

  defp symbol_position_adjacent([], _range), do: false

  defp symbol_position_adjacent([hd | tl], range) do
    if hd in range do
      true
    else
      symbol_position_adjacent(tl, range)
    end
  end
end
```

```elixir
Part1.solution("""
467..114..
...*......
..35..633.
......#...
617*......
.....+.58.
..592.....
......755.
...$.*....
.664.598..
""")
```

```elixir
input1 = Kino.Input.textarea("Full Input")
```

```elixir
lines1 = Kino.Input.read(input1)
Part1.solution(lines1)
```

## Part 2

The engineer finds the missing part and installs it in the engine! As the engine springs to life, you jump in the closest gondola, finally ready to ascend to the water source.

You don't seem to be going very fast, though. Maybe something is still wrong? Fortunately, the gondola has a phone labeled "help", so you pick it up and the engineer answers.

Before you can explain the situation, she suggests that you look out the window. There stands the engineer, holding a phone in one hand and waving with the other. You're going so slowly that you haven't even left the station. You exit the gondola.

The missing part wasn't the only issue - one of the gears in the engine is wrong. A gear is any * symbol that is adjacent to exactly two part numbers. Its gear ratio is the result of multiplying those two numbers together.

This time, you need to find the gear ratio of every gear and add them all up so that the engineer can figure out which gear needs to be replaced.

Consider the same engine schematic again:

```
467..114..
...*......
..35..633.
......#...
617*......
.....+.58.
..592.....
......755.
...$.*....
.664.598..
```

In this schematic, there are two gears. The first is in the top left; it has part numbers 467 and 35, so its gear ratio is 16345. The second gear is in the lower right; its gear ratio is 451490. (The * adjacent to 617 is not a gear because it is only adjacent to one part number.) Adding up all of the gear ratios produces 467835.

```elixir
defmodule Part2 do
  def solution(input) do
    input
    |> String.trim()
    |> String.split("\n")
    |> Enum.map(&Parser.parse_line/1)
    |> find_star_numbers(nil, [])
    |> Enum.sum()
  end

  defp find_star_numbers([], _, agg), do: agg

  defp find_star_numbers([hd | tl], previous_line, agg) do
    value_in_head =
      hd.adjacents
      |> Enum.map(fn star ->
        possible_numbers(star, [previous_line, hd, List.first(tl)], [])
      end)
      |> Enum.filter(fn s -> Enum.count(s) == 2 end)
      |> Enum.map(fn [n1, n2] -> n1.number * n2.number end)
      |> dbg()

    find_star_numbers(tl, hd, agg ++ value_in_head)
  end

  defp possible_numbers(_position, [], agg), do: agg

  defp possible_numbers(position, [%{numbers: numbers} | tl], agg) do
    possible_number_matches =
      numbers
      |> Enum.filter(fn %{start: min, end: max} ->
        position in (min - 1)..(max + 1)
      end)

    possible_numbers(position, tl, agg ++ possible_number_matches)
  end
end
```

```elixir
Part2.solution("""
467..114..
...*......
..35..633.
......#...
617*......
.....+.58.
..592.....
......755.
...$.*....
.664.598..
""")
```

## Solutions

```elixir
Part2.solution(lines1)
```
