# Day 2

```elixir
Mix.install([
  {:kino, "~> 0.11.3"}
])
```

## Part 1

You're launched high into the atmosphere! The apex of your trajectory just barely reaches the surface of a large island floating in the sky. You gently land in a fluffy pile of leaves. It's quite cold, but you don't see much snow. An Elf runs over to greet you.

The Elf explains that you've arrived at Snow Island and apologizes for the lack of snow. He'll be happy to explain the situation, but it's a bit of a walk, so you have some time. They don't get many visitors up here; would you like to play a game in the meantime?

As you walk, the Elf shows you a small bag and some cubes which are either red, green, or blue. Each time you play this game, he will hide a secret number of cubes of each color in the bag, and your goal is to figure out information about the number of cubes.

To get information, once a bag has been loaded with cubes, the Elf will reach into the bag, grab a handful of random cubes, show them to you, and then put them back in the bag. He'll do this a few times per game.

You play several games and record the information from each game (your puzzle input). Each game is listed with its ID number (like the 11 in Game 11: ...) followed by a semicolon-separated list of subsets of cubes that were revealed from the bag (like 3 red, 5 green, 4 blue).

For example, the record of a few games might look like this:

```
Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green
```

In game 1, three sets of cubes are revealed from the bag (and then put back again). The first set is 3 blue cubes and 4 red cubes; the second set is 1 red cube, 2 green cubes, and 6 blue cubes; the third set is only 2 green cubes.

The Elf would first like to know which games would have been possible if the bag contained only 12 red cubes, 13 green cubes, and 14 blue cubes?

In the example above, games 1, 2, and 5 would have been possible if the bag had been loaded with that configuration. However, game 3 would have been impossible because at one point the Elf showed you 20 red cubes at once; similarly, game 4 would also have been impossible because the Elf showed you 15 blue cubes at once. If you add up the IDs of the games that would have been possible, you get 8.

Determine which games would have been possible if the bag had been loaded with only 12 red cubes, 13 green cubes, and 14 blue cubes. What is the sum of the IDs of those games?

## Solution

```elixir
defmodule Parser do
  defmodule Game do
    # Cubes are a list of tuples, each tuple in {r, g, b}
    defstruct [:id, :cubes]
  end

  @game_regex ~r/Game (?<id>\d+): (?<cubes>.+)/

  def parse_line(line) do
    %{"id" => id, "cubes" => cubes} = parse_with_regex(line)

    %Parser.Game{id: String.to_integer(id), cubes: parse_cubes(cubes)}
  end

  defp parse_with_regex(line) do
    Regex.named_captures(@game_regex, line)
  end

  defp parse_cubes(cubes) do
    cubes
    |> String.split(";")
    |> Enum.map(&parse_cube/1)
  end

  defp parse_cube(cube) do
    cube_map =
      cube
      |> String.split(",")
      |> Enum.map(&String.trim/1)
      |> Enum.map(&parse_color_with_number/1)
      |> Enum.into(%{})

    {Map.get(cube_map, :red, 0), Map.get(cube_map, :green, 0), Map.get(cube_map, :blue, 0)}
  end

  defp parse_color_with_number(color_with_number) do
    [number, color] = String.split(color_with_number, " ")
    {String.to_atom(color), String.to_integer(number)}
  end
end
```

```elixir
Parser.parse_line("Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green")
```

```elixir
defmodule Part1 do
  def solution(input) do
    input
    |> String.trim()
    |> String.split("\n")
    |> Enum.map(&line/1)
    |> Enum.filter(fn i -> not is_nil(i) end)
    |> Enum.sum()
  end

  # return id if the game is viable or nil else
  def line(input) do
    %{id: id, cubes: cubes} = Parser.parse_line(input)
    if Enum.all?(cubes, &viable/1), do: id, else: nil
  end

  defp viable({red, green, blue}) do
    red <= 12 and green <= 13 and blue <= 14
  end
end
```

```elixir
Part1.line("Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green")
```

```elixir
Part1.line("Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue")
```

```elixir
Part1.line("Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red")
```

```elixir
Part1.line("Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red")
```

```elixir
Part1.line("Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green")
```

```elixir
Part1.solution("""
Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green
""")
```

## Part 2

The Elf says they've stopped producing snow because they aren't getting any water! He isn't sure why the water stopped; however, he can show you how to get to the water source to check it out for yourself. It's just up ahead!

As you continue your walk, the Elf poses a second question: in each game you played, what is the fewest number of cubes of each color that could have been in the bag to make the game possible?

Again consider the example games from earlier:

```
Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green
```

In game 1, the game could have been played with as few as 4 red, 2 green, and 6 blue cubes. If any color had even one fewer cube, the game would have been impossible.

Game 2 could have been played with a minimum of 1 red, 3 green, and 4 blue cubes.

Game 3 must have been played with at least 20 red, 13 green, and 6 blue cubes.

Game 4 required at least 14 red, 3 green, and 15 blue cubes.

Game 5 needed no fewer than 6 red, 3 green, and 2 blue cubes in the bag.

The power of a set of cubes is equal to the numbers of red, green, and blue cubes multiplied together. The power of the minimum set of cubes in game 1 is 48. In games 2-5 it was 12, 1560, 630, and 36, respectively. Adding up these five powers produces the sum 2286.

For each game, find the minimum set of cubes that must have been present.

```elixir
defmodule Part2 do
  def solution(input) do
    input
    |> String.trim()
    |> String.split("\n")
    |> Enum.map(&line/1)
    |> Enum.map(fn {red, green, blue} -> red * green * blue end)
    |> Enum.sum()
  end

  def line(input) do
    %{cubes: cubes} = Parser.parse_line(input)
    calculate_minimal_cube(cubes, {0, 0, 0})
  end

  defp calculate_minimal_cube([], {red, green, blue}) do
    {red, green, blue}
  end

  defp calculate_minimal_cube([{red, green, blue} | tl], {min_red, min_green, min_blue}) do
    calculate_minimal_cube(
      tl,
      {max(red, min_red), max(green, min_green), max(blue, min_blue)}
    )
  end
end
```

```elixir
Part2.line("Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green")
```

```elixir
Part2.line("Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue")
```

```elixir
Part2.line("Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red")
```

```elixir
Part2.line("Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red")
```

```elixir
Part2.line("Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green")
```

```elixir
Part2.solution("""
Game 1: 3 blue, 4 red; 1 red, 2 green, 6 blue; 2 green
Game 2: 1 blue, 2 green; 3 green, 4 blue, 1 red; 1 green, 1 blue
Game 3: 8 green, 6 blue, 20 red; 5 blue, 4 red, 13 green; 5 green, 1 red
Game 4: 1 green, 3 red, 6 blue; 3 green, 6 red; 3 green, 15 blue, 14 red
Game 5: 6 red, 1 blue, 3 green; 2 blue, 1 red, 2 green
""")
```

## Solutions

```elixir
input1 = Kino.Input.textarea("Full Input")
```

```elixir
lines1 = Kino.Input.read(input1)
```

```elixir
Part1.solution(lines1)
```

```elixir
Part2.solution(lines1)
```
